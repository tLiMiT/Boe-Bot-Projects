' Team 9 - Car Follower Boe-Bot
' Tim Liming - Joe Robinson - Matt van Berlo

' {$STAMP BS2}
' {$PBASIC 2.5}

'Signal program start / reset
FREQOUT 15, 200, 2500: FREQOUT 15, 200, 2000
FREQOUT 15, 200, 2500: FREQOUT 15, 700, 3000

'Set Pins -------------------------------------------------------------------
PING_Left         PIN   3
PING_Right_Trig   PIN   4
PING_Right_Echo   PIN   5
Red_LED           PIN   9
Green_LED         PIN   10
Blue_LED          PIN   11
Right_Motor       PIN   12
Left_Motor        PIN   13

'Set Constants --------------------------------------------------------------
servoCenter       CON   750   ' servo is stopped
MinDist           CON   8     ' minimum distance to car
MaxDist           CON   15    ' maximum distance to car
Trigger           CON   5     ' send a pulse to PING)))
Centimeters       CON   2251  ' convert PING data to centimeters
setHigh           CON   1     ' Logic HIGH
setLow            CON   0     ' Logic LOW

'Declare Variables ----------------------------------------------------------
distLeft          VAR   Word
distRight         VAR   Word
pulseLeft         VAR   Word
pulseRight        VAR   Word


'Main Loop ------------------------------------------------------------------
DO

  'Set RGB LED Pins low
  LOW Red_LED: LOW Green_LED: LOW Blue_LED

  'Get the distances
  GOSUB getDistances

  'Handle Left PING input
  IF (MaxDist <= distLeft) THEN
    pulseLeft = 850
  ELSEIF (MinDist > distLeft) THEN
    pulseLeft = 650
  ELSE
    pulseLeft = servoCenter
  ENDIF

  'Handle Right PING input
  IF (MaxDist <= distRight) THEN
    pulseRight = 650
  ELSEIF (MinDist > distRight) THEN
    pulseRight = 850
  ELSE
    pulseRight = servoCenter
  ENDIF

  'Send pulses to drive motors
  GOSUB sendMotorPulse

LOOP


' Subroutines ---------------------------------------------------------------

'Subroutine: getDistances
getDistances:
  PULSOUT PING_Left, Trigger
  PULSIN  PING_Left, setHigh, distLeft

  PAUSE 5

  PULSOUT PING_Right_Trig, Trigger
  PULSIN  PING_Right_Echo, setHigh, distRight

  distLeft = distLeft ** Centimeters
  distRight = distRight ** Centimeters

  RETURN

'Subroutine: sendMotorPulse
sendMotorPulse:
  IF ((pulseLeft = 850) & (pulseRight = 650)) | ((pulseLeft = 650) & (pulseRight = 850)) THEN
    HIGH Green_LED      ' Forward | Reverse
  ELSEIF ((pulseLeft = 850) & (pulseRight = 850)) | ((pulseLeft = 850) & (pulseRight = 750)) THEN
    HIGH Red_LED        ' Right
  ELSEIF ((pulseLeft = 650) & (pulseRight = 650)) | ((pulseLeft = 750) & (pulseRight = 650)) THEN
    HIGH Blue_LED       ' Left
  ENDIF

  PULSOUT Left_Motor,  pulseLeft
  PULSOUT Right_Motor, pulseRight
  PAUSE 5

  RETURN